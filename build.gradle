plugins {
    id 'java'
    id 'idea'
    id 'maven-publish'
}

repositories {
    mavenCentral()
}

group = 'estar'
version = '1.6.2-ESTAR'
sourceCompatibility = 1.8
targetCompatibility = 1.8

compileJava {
    options.compilerArgs += ['-encoding', 'UTF-8']
}

javadoc {
    options.encoding = 'UTF-8'
}

publishing {
    publications {
        maven(MavenPublication) {
            groupId = project.group
            artifactId = 'java-websocket'
            version = project.version

            from components.java
        }
    }
    repositories {
        maven {
            url 'https://pkgs.dev.azure.com/e-star-trading/_packaging/Externals/maven/v1'
            name 'e-star-trading'
            credentials {
                username "e-star-trading"
                password "$System.env.AZURE_PERSONAL_ACCESS_TOKEN"
            }
            authentication {
                basic(BasicAuthentication)
            }
        }
    }
}

dependencies {
    implementation group: 'org.slf4j', name: 'slf4j-api', version: '2.0.15'
    implementation group: 'org.jctools', name: 'jctools-core', version: '4.0.1'
    testImplementation group: 'org.slf4j', name: 'slf4j-simple', version: '2.0.15'
    testImplementation group: 'org.junit.jupiter', name: 'junit-jupiter', version: '5.11.4'
}

test {
    useJUnitPlatform()  // This is required for JUnit 5
}
